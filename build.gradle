/*
 * Copyright 2009 Qbranch AB
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

usePlugin 'java'

repositories {
	mavenCentral()
}

dependencies {
	compile group: 'org.mortbay.jetty', name: 'jetty-embedded', version: '6.1.21'
}

defaultTasks 'buildStandaloneWar'

def depsDir = file('build/dependencies')
def outDir = file('build/out')
def warDir = file('build/explodedWar')

task extractDependencies << {
  	for(file in configurations.runtime) {
		ant.unzip(src: file.absolutePath,
			      dest: depsDir)
	}
	
	ant.delete(dir: new File(depsDir, 'META-INF'))
}

task buildStarter(dependsOn: extractDependencies) << {

	outDir.mkdirs()
	
	ant.javac(srcdir:'src', destdir: outDir,
			  fork: 'true', classpath: depsDir)
	
}

task buildStandaloneWar(dependsOn: buildStarter) << {
    if(project.hasProperty('inWar') && !project.hasProperty('outWar')) {
      def warName = inWar[0..(inWar.indexOf('.war')-1)]

      outWar = "build/$warName-standalone.war"
    }

	ant.unzip(src: inWar, dest: warDir)
	
	copy {
    	from depsDir
		from outDir
		into warDir
    }

	ant.manifest(file: new File(warDir, 'META-INF/MANIFEST.MF')) {
		attribute(name: 'Main-Class', value: 'se.qbranch.standalone.Starter')
	}

	ant.zip(destfile: "$outWar") {
		fileset(dir: warDir)
	}
}